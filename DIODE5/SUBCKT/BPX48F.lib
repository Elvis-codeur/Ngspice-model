
*SRC=BPX48F;BPX48F;Diodes;Photo;PIN, 650MA/W
*SYM=BPW34
.SUBCKT BPX48F  4             1        3
*               INPUT Pwr(W)  CATHODE  ANODE
D2  6  1  BPX48F
G2  1  6  POLY(2)  1  6  4  0  0    1N  650M
* G2  1  6  POLY(2)  1  6  4  0  0  IDARK  SENS
* Dark current as a function of VR: IDARK*V(1,6) 
* and Spectral Photosensitivity (A/W): SENS at 950NM
V4  6  3 ;  Photodiode Current
.MODEL BPX48F D (IS=951P RS=42M N=2.85 BV=10 IBV=10U
+ CJO=25P VJ=.75 M=.333 TT=720N)
*  10 Volt  Si Photo Diode  08-23-1994
.ENDS
