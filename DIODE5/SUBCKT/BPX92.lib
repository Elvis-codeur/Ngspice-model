
*SRC=BPX92;BPX92;Diodes;Photo;PIN, 500MA/W
*SYM=BPW34
.SUBCKT BPX92   4             1        3
*               INPUT Pwr(W)  CATHODE  ANODE
D2  6  1  BPX92
G2  1  6  POLY(2)  1  6  4  0  0  110P  500M
* G2  1  6  POLY(2)  1  6  4  0  0  IDARK  SENS
* Dark current as a function of VR: IDARK*V(1,6) 
* and Spectral Photosensitivity (A/W): SENS at 850NM
V4  6  3 ;  Photodiode Current
.MODEL BPX92 D (IS=951P RS=42M N=2.85 BV=32 IBV=10U
+ CJO=90P VJ=.75 M=.333 TT=2.16U)
*  32 Volt  Si Photo Diode  08-23-1994
.ENDS
