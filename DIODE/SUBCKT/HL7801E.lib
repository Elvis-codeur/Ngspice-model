
*SRC=HL7801E;HL7801E;Diodes;Laser;Hitachi
*SYM=LASER
*HELP issyntax.hlp "Laser Diode Models"
.SUBCKT HL7801E 2 3 11 10 
*This subckt accounts for threshold temperature coefficient
*Use .TEMP statement for the appropriate junction temperature
D1 1 3 DLASER
.MODEL DLASER D(RS=1.5 EG=2.8 XTI=3 N=1 IS=1E-30)       
H1 7 0 V1 24
D2 8 9 DIODE3
.MODEL DIODE3 D(EG=.1)
R1 9 0 100
G1 10 11 9 0 .0012
D3 11 10 DIODE
.MODEL DIODE D(CJO=5PF) 
*SPICE 2G syntax
*R3 7 8 1 TC=.0085
*IsSpice3 syntax
R3 7 8 1 RMOD
.MODEL RMOD R TC1=.0085
I2 8 7 .9
C1 9 0 50P
V1 2 1 
.ENDS
