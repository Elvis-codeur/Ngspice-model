
*SRC=IGTH10N40D;IGTH104D;IGBTs;w/Diode;400V 10A
*SYM=IGBT
.SUBCKT IGTH104D  10 20 30
*     TERMINALS:  C  G  E
Q1  30  1 10     QOUT
M1   1  2 30 30  MFIN L=1U W=1U
RG  20  2  15
CGD  4  1  58.2P
RGD  4  1  10MEG
MGD  4  5  2  2  SW L=1U W=1U
EGD  5  2  2  1  1
DDG  2  6  DCGD
MDG  6  7  1  1  SW L=1U W=1U
EDG  7  1  1  2  1
DDS 30  1  DSUB
VEC  8 10  3.5
DEC  1  8  DBE
DFW  30 10 GTH4D
.MODEL QOUT PNP (IS=2.18P BF=3.65 RE=32.7M TF=5.37N TR=510N)
.MODEL MFIN NMOS (LEVEL=3 VMAX=15.6MEG THETA=70M VTO=3.0
+ KP=.592 IS=2.18P RS=15.2M CGSO=512U)
.MODEL SW NMOS (LEVEL=3 VTO=0 KP=.011)
.MODEL DCGD D (CJO=58.2P VJ=.41 M=.5)
.MODEL DSUB D (IS=2.18P BV=399 IBV=1M CJO=39.3P VJ=.8 M=.4 TT=432P)
.MODEL DBE  D (IS=2.18P)
.MODEL GTH4D D (IS=15.5U RS=14.4M N=3.01 CJO=412P VJ=.75 M=.333 TT=57.6U)
*  400 Volt  10 Amp  N-Channel IGBT  07-27-1991
.ENDS
