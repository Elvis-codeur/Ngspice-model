
*SRC=IGTH20N40;IGTH204;IGBTs;No Diode;400V 20A
*SYM=IGBT
.SUBCKT IGTH204 10 20 30
*     TERMINALS:  C  G  E
Q1  30  1 10     QOUT
M1   1  2 30 30  MFIN L=1U W=1U
RG  20  2  7.5
CGD  4  1  164P
RGD  4  1  10MEG
MGD  4  5  2  2  SW L=1U W=1U
EGD  5  2  2  1  1
DDG  2  6  DCGD
MDG  6  7  1  1  SW L=1U W=1U
EDG  7  1  1  2  1
DDS 30  1  DSUB
VEC  8 10  3.5
DEC  1  8  DBE
.MODEL QOUT PNP (IS=4.37P BF=3.65 RE=16.3M TF=7.16N TR=510N)
.MODEL MFIN NMOS (LEVEL=3 VMAX=15.6MEG THETA=70M VTO=3.0
+ KP=1.293 IS=4.37P RS=7.61M CGSO=1.12M)
.MODEL SW NMOS (LEVEL=3 VTO=0 KP=13.1M)
.MODEL DCGD D (CJO=164P VJ=.41 M=.5)
.MODEL DSUB D (IS=4.37P BV=399 IBV=1M CJO=111P VJ=.8 M=.4 TT=432P)
.MODEL DBE  D (IS=4.37P)
*  400 Volt  20 Amp  N-Channel IGBT  07-27-1991
.ENDS
