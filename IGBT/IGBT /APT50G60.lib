
*SRC=APT50G60BN;APT50G60;IGBTs;No Diode;600V 50A
*SYM=IGBT
.SUBCKT APT50G60 10 20 30
*     TERMINALS:  C  G  E
Q1  30  1 10     QOUT
M1   1  2 30 30  MFIN L=1U W=1U
RG  20  2  3
CGD  4  1  630P
RGD  4  1  10MEG
MGD  4  5  2  2  SW L=1U W=1U
EGD  5  2  2  1  1
DDG  2  6  DCGD
MDG  6  7  1  1  SW L=1U W=1U
EDG  7  1  1  2  1
DDS 30  1  DSUB
VEC  8 10  23.5
DEC  1  8  DBE
.MODEL QOUT PNP (IS=8.33P BF=2.1 RE=11.6M TF=35.8N TR=1.02U)
.MODEL MFIN NMOS (LEVEL=1 VTO=3.9
+ KP=.888 IS=8.33P RS=3.61M CGSO=2.02M)
.MODEL SW NMOS (LEVEL=1 VTO=0 KP=229.4M)
.MODEL DCGD D (CJO=630P VJ=.41 M=.5)
.MODEL DSUB D (IS=8.33P BV=599 IBV=1M CJO=321P VJ=.8 M=.4 TT=864P)
.MODEL DBE  D (IS=8.33P)
*  600 Volt  50 Amp  N-Channel IGBT  07-27-1991
.ENDS
