
*SRC=IRGBC40U;IRGBC40U;IGBTs;No Diode;600V 40A
*SYM=IGBT
.SUBCKT IRGBC40U 10 20 30
*     TERMINALS:  C  G  E
Q1  30  1 10     QOUT
M1   1  2 30 30  MFIN L=1U W=1U
RG  20  2  3.75
CGD  4  1  146P
RGD  4  1  10MEG
MGD  4  5  2  2  SW L=1U W=1U
EGD  5  2  2  1  1
DDG  2  6  DCGD
MDG  6  7  1  1  SW L=1U W=1U
EDG  7  1  1  2  1
DDS 30  1  DSUB
VEC  8 10  13.5
DEC  1  8  DBE
.MODEL QOUT PNP (IS=6.36P BF=3.8 RE=7.51M TF=4.11N TR=295N)
.MODEL MFIN NMOS (LEVEL=1 VTO=4.2
+ KP=.987 IS=6.36P RS=3.61M CGSO=1.48M)
.MODEL SW NMOS (LEVEL=1 VTO=0 KP=.168)
.MODEL DCGD D (CJO=146P VJ=.41 M=.5)
.MODEL DSUB D (IS=6.36P BV=599 IBV=1M CJO=97.1P VJ=.8 M=.4 TT=250P)
.MODEL DBE  D (IS=6.36P)
*  600 Volt  40 Amp  N-Channel IGBT  08-19-1991
.ENDS
