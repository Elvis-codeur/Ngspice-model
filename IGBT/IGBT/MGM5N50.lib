
*SRC=MGM5N50;MGM5N50;IGBTs;No Diode;500V 5A
*SYM=IGBT
.SUBCKT MGM5N50  10 20 30
*     TERMINALS:  C  G  E
Q1  30  1 10     QOUT
M1   1  2 30 30  MFIN L=1U W=1U
RG  20  2  30
CGD  4  1  78.7P
RGD  4  1  10MEG
MGD  4  5  2  2  SW L=1U W=1U
EGD  5  2  2  1  1
DDG  2  6  DCGD
MDG  6  7  1  1  SW L=1U W=1U
EDG  7  1  1  2  1
DDS 30  1  DSUB
VEC  8 10  13.5
DEC  1  8  DBE
.MODEL QOUT PNP (IS=946F BF=2.1 RE=.108 TF=71.6N TR=340N)
.MODEL MFIN NMOS (LEVEL=3 VMAX=18.2MEG THETA=60M VTO=3.5
+ KP=.208 IS=946F RS=33.6M CGSO=290U RD=.2)
.MODEL SW NMOS (LEVEL=3 VTO=0 KP=212.9M)
.MODEL DCGD D (CJO=78.7P VJ=.41 M=.5)
.MODEL DSUB D (IS=946F BV=499 IBV=1M CJO=40.1P VJ=.8 M=.4 TT=288P)
.MODEL DBE  D (IS=946F)
*Motorola 500 Volt  5 Amp  N-Channel IGBT  07-27-1991
.ENDS
